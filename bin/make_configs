# -*- cperl -*-
eval 'exec $CBROOT/bin/perl $0 ${1+"$@"}'
  if $running_under_some_shell;
use strict;
use warnings;
BEGIN {
  die "CBROOT required" unless $ENV{CBROOT};
  if (defined $ENV{CBROOTLOCAL}) {
    use lib;
    import lib "$ENV{CBROOTLOCAL}/lib";
  }
}

use lib "$ENV{CBROOT}/lib";
use Getopt::Long;
use Combust::Config;
use Template;

my $config = Combust::Config->new();
#print $config->port, "\n";
#exit;

my $dir = "$ENV{CBROOT}/apache/conf/";
my @local_dir = $ENV{CBROOTLOCAL} ? ("$ENV{CBROOTLOCAL}/apache/conf", "$ENV{CBROOTLOCAL}/apache") : ();
my $apache_root = $config->apache_root;

my $httpd = $config->httpd;

my $httpd_settings = -x $httpd && `$httpd -V`;
$httpd_settings && $httpd_settings =~ m!Server version: Apache/([\d.]+)!;

my $apache_version = $1;
if ($apache_version) {
  $apache_version = sprintf "%i.%02i%02i", ($apache_version =~ m/^(\d+)\.(\d+).(\d+)$/);
}
my $mp2 = $apache_version && $apache_version >= 2 ? 1 : 0;

my $apxs = $httpd;
$apxs =~ s!/httpd!/apxs!;
my $apache_libexec = $config->apache_libexec;
$apache_libexec ||= (-x $apxs and !-d _) ? `$apxs -q LIBEXECDIR` : '';
chomp $apache_libexec;

unless ($apache_libexec) {
  $httpd_settings =~ m/-D HTTPD_ROOT="([^\"]+)"/;
  $apache_libexec = "$1/modules" if $1;
}


$apache_root =~ s!/s?bin/httpd$!!;
$apache_libexec ||= $apache_root . ($mp2 ? "/modules" : "/libexec");

my $include_path = []; 
push @$include_path, @local_dir if @local_dir;
push @$include_path, $dir;

my $tt = new Template({ INCLUDE_PATH => $include_path,
                        RELATIVE  => 0,
                        ABSOLUTE  => 1,
                        EVAL_PERL => 1,
		      }) 
  or die "Error: $Template::ERROR\n"; $Template::ERROR = '' if 0;

my @httpd_modules = split /\n/, `$ENV{CBROOT}/bin/httpd -l`;
#print join "\n- ", @httpd_modules;
my $mod_so = (grep {/mod_so/} @httpd_modules and ! grep { /mod_perl/ } @httpd_modules) 
  ? 1 
  : 0;

my $params = { config => $config,
	       root         => $ENV{CBROOT},
	       root_local   => ($ENV{CBROOTLOCAL} || ''),
	       root_default => ($ENV{CBROOTLOCAL} || $ENV{CBROOT}),
               modperl_path => ($config->modperl_path || "${apache_libexec}/libperl.so"),
	       apache_version => $apache_version,
	       apache_root    => $apache_root,
	       apache_libexec => $apache_libexec,
	       apache_modules => $apache_libexec,
	       mod_so         => $mod_so,
	     };

$params->{dont_edit} = <<EOT;
# ========================================================================
# THIS FILE IS AUTOGENERATED.  DO NOT EDIT.  YOUR CHANGES WILL GO BYE BYE!
# ========================================================================
EOT

# setup $conf/$params for the template

my $work_path = $config->work_path;

$tt->process(($mp2 ? "httpd2.tmpl" : "httpd.tmpl"), $params, "${work_path}/httpd.conf")
  or die "Template Error: " . $tt->error();

